/*
Inserts the Average GCSE Score (used by ALPS) into StudentDetail UserDefined23
Ensure this field does not contain existing data and, if it does, switch to using another vacant field
*/

CREATE PROCEDURE [dbo].[SPR_ALP_UpdateAvgPointsScores]
AS
BEGIN
	SET NOCOUNT ON;

	DROP TABLE IF EXISTS #EarliestStart
	SELECT
		SD.StudentDetailID,
		EarliestStartDate = MIN ( ENR.StartDate )
		INTO #EarliestStart
	FROM ProSolution.dbo.StudentDetail SD
	INNER JOIN ProSolution.dbo.Enrolment ENR
		ON ENR.StudentDetailID = SD.StudentDetailID
	WHERE
		SD.AcademicYearID = (
			SELECT
				CFG.Value
			FROM Config CFG
			WHERE
				CFG.ConfigID = 'PMO_AcademicYearID'
		)
		AND ENR.CompletionStatusID IN ( '1', '2', '3', '4' )
		AND ENR.IncludeInReturn = 1
	GROUP BY
		SD.StudentDetailID


	UPDATE SD
	SET
		SD.UserDefined23 = FORMAT ( COALESCE ( PLR.AvgGCSE, QOE.AvgGCSE ), '#0.00' )
	FROM ProSolution.dbo.StudentDetail SD
	LEFT JOIN (
		SELECT
			StudentID = QOE.StudentID,
			NumGCSEs = COUNT ( QOE.StudentID ),
			TotalSize = SUM ( QOE.GradeSize ),
			AvgGCSE = CAST ( ROUND ( CAST ( SUM ( QOE.Points ) AS FLOAT ) / CAST ( SUM ( QOE.GradeSize ) AS FLOAT ), 2 ) AS DECIMAL (10, 2) )
		FROM (
			SELECT DISTINCT
				PLR.StudentID,
				PLRD.SourceQualificationCode,
				PLRD.QualificationTitle,
				PLRD.AwardDate,
				PLRD.Grade,
				GP.Points,
				GradeSize = 
					CASE
						WHEN PLRD.QualificationTitle LIKE '%Short%' THEN 0.5
						WHEN PLRD.QualificationTitle LIKE '%Double%' THEN 2.0
						WHEN PLRD.QualificationTitle LIKE '%Triple%' THEN 3.0
						WHEN LEN ( REPLACE ( PLRD.Grade, '*', '' ) ) = 2 THEN 2.0
						WHEN LEN ( REPLACE ( PLRD.Grade, '*', '' ) ) = 3 THEN 3.0
						WHEN LEN ( REPLACE ( PLRD.Grade, '*', '' ) ) = 4 THEN 4.0
						ELSE 1.0
					END,
				GradeOrder = 
					ROW_NUMBER () OVER ( 
						PARTITION BY 
							PLR.StudentID, 
							PLRD.SourceQualificationCode
						ORDER BY 
							CASE
								WHEN PLRD.Grade = 'A*' THEN 1
								ELSE 2
							END,
							PLRD.Grade 
					)
			FROM ProSolution.dbo.StudentDetail SD
			INNER JOIN ProSolution.dbo.PLRDownload PLR
				ON PLR.StudentID = SD.StudentID
			INNER JOIN ProSolution.dbo.PLRLearnerEventDownload PLRD
				ON PLRD.PLRDownloadID = PLR.PLRDownloadID
			INNER JOIN #EarliestStart EAR
				ON EAR.StudentDetailID = SD.StudentDetailID
			INNER JOIN GradePoints GP
				ON GP.Grade = PLRD.Grade
				AND GP.QualType = 'G'
			LEFT JOIN ProSolution.dbo.Learning_Aim AIM
				ON AIM.LEARNING_AIM_REF = PLRD.SourceQualificationCode
			WHERE
				SD.AcademicYearID = (
					SELECT
						CFG.Value
					FROM Config CFG
					WHERE
						CFG.ConfigID = 'PMO_AcademicYearID'
				)
				AND (
					PLRD.QualificationTitle LIKE '%GCSE%'
					OR AIM.LEARNING_AIM_TITLE LIKE '%GCSE%'
				)
				AND PLRD.AwardDate IS NOT NULL
				AND PLRD.AwardDate < EAR.EarliestStartDate
		) QOE
		WHERE
			QOE.GradeOrder = 1
			--AND QOE.StudentID = 75672
		GROUP BY
			QOE.StudentID
	) PLR
		ON PLR.StudentID = SD.StudentID
	LEFT JOIN (
		SELECT
			StudentID = QOE.StudentID,
			NumGCSEs = COUNT ( QOE.StudentID ),
			AvgGCSE = CAST ( ROUND ( CAST ( SUM ( QOE.Points ) AS FLOAT ) / CAST ( SUM ( QOE.GradeSize ) AS FLOAT ), 2 ) AS DECIMAL (10, 2) )
		FROM (
			SELECT
				StudentID = SD.StudentID,
				AimCode = QOE.QualCode,
				AimTitle = COALESCE ( AIM.LEARNING_AIM_TITLE, QOEQ.Title, QOE.Subject ),
				Grade =  QOE.Grade,
				Points = GP.Points,
				GradeSize = 
					CASE
						WHEN COALESCE ( AIM.LEARNING_AIM_TITLE, QOEQ.Title, QOE.Subject ) LIKE '%Short%' THEN 0.5
						WHEN COALESCE ( AIM.LEARNING_AIM_TITLE, QOEQ.Title, QOE.Subject ) LIKE '%Double%' THEN 2.0
						WHEN COALESCE ( AIM.LEARNING_AIM_TITLE, QOEQ.Title, QOE.Subject ) LIKE '%Triple%' THEN 3.0
						WHEN LEN ( REPLACE ( QOE.Grade, '*', '' ) ) = 2 THEN 2.0
						WHEN LEN ( REPLACE ( QOE.Grade, '*', '' ) ) = 3 THEN 3.0
						WHEN LEN ( REPLACE ( QOE.Grade, '*', '' ) ) = 4 THEN 4.0
						ELSE 1.0
					END,
				GradeOrder = 
					ROW_NUMBER () OVER ( 
						PARTITION BY 
							SD.StudentID, 
							QOE.QualCode
						ORDER BY 
							CASE
								WHEN QOE.Grade = 'A*' THEN 1
								ELSE 2
							END,
							QOE.Grade 
					)
			FROM ProSolution.dbo.StudentDetail SD
			INNER JOIN ProSolution.dbo.QualsOnEntry QOE
				ON QOE.StudentID = SD.StudentID
			INNER JOIN #EarliestStart EAR
				ON EAR.StudentDetailID = SD.StudentDetailID
			LEFT JOIN ProSolution.dbo.QualsOnEntryAvailableQuals QOEQ
				ON QOEQ.Code = QOE.QualCode
			INNER JOIN GradePoints GP
				ON GP.Grade = QOE.Grade
				AND GP.QualType = 'G'
			LEFT JOIN ProSolution.dbo.Learning_Aim AIM
				ON AIM.LEARNING_AIM_REF = QOE.QualCode
			WHERE
				SD.AcademicYearID = (
					SELECT
						CFG.Value
					FROM Config CFG
					WHERE
						CFG.ConfigID = 'ALP_AcademicYearID'
				)
				AND QOE.EvidenceSeen = 1
				AND COALESCE ( AIM.LEARNING_AIM_TITLE, QOEQ.Title, QOE.Subject ) LIKE '%GCSE%'
				AND COALESCE ( QOE.DateAwarded, EAR.EarliestStartDate ) <= EAR.EarliestStartDate
				AND QOE.Grade IS NOT NULL
		) QOE
		WHERE
			QOE.GradeOrder = 1
			--AND QOE.StudentID = 75672
		GROUP BY
			QOE.StudentID
	) QOE
		ON QOE.StudentID = SD.StudentID
	WHERE
		SD.AcademicYearID = (
			SELECT
				CFG.Value
			FROM Config CFG
			WHERE
				CFG.ConfigID = 'PMO_AcademicYearID'
		)
		AND COALESCE ( SD.UserDefined23, '' ) <> COALESCE ( FORMAT ( COALESCE ( PLR.AvgGCSE, QOE.AvgGCSE ), '#0.00' ), '' ) --Not already recorded
END